# $Id: man2pea.sno,v 1.4 2014/12/06 05:25:14 phil Exp $
# make a quick cut at converting a man page to "snopea" format
# Phil Budne, November 30, 2014

	&anchor = 1

	tab = char(9)
	ws = span(' ' tab)
	breakws = break(' ' tab)
	twofontcmd = '.' ANY('RBI') . f1 ANY('RBI') . f2 ws rem . rest
	onefontcmd = '.' ANY('BI') . font ws rem . rest

	q = '"'
	tokpat = ((q break(q) . text q) | breakws . text) ws

	charpats = '\*(lq' | '\*(rq' | '\&' | ANY('<>&') | '\*(pi' | '\0'
	charmap = TABLE()
	charmap['\*(lq'] = "``"
	charmap['\*(rq'] = "''"
	charmap['\*(pi'] = '&pi;'
	charmap['\0'] = '\~'
	charmap['\&'] = '\&'
	charmap['&'] = '&amp;'
	charmap['<'] = '&lt;'
	charmap['>'] = '&gt;'

****************
start	line = input					:f(end)
	line '.TH'					:f(start)

loop	prefix =
loop1	line = input					:f(end)

*	TERMINAL = '>>' line
* handle character entities
	oline =
cloop	line POS(0) ARB . pre charpats . chr =		:f(cdone)
*	TERMINAL = oline '/' pre '/' chr

	oline = oline pre charmap[chr]			:(cloop)
cdone	line =  oline line
	line '.'					:f(notcmd)
	line twofontcmd					:s(twofontcmd)
	line onefontcmd					:s(onefontcmd)
	line '.SH'					:s(shcmd)
	line '.SS'					:s(sscmd)
	line '.PP'					:s(ppcmd)
	line '.LP'					:s(lpcmd)
	line '.TP'					:s(lpcmd)
	line '.IP'					:s(lpcmd)
	line '.IP \(bu'					:s(bullcmd)
	line '.br'					:s(brcmd)

	terminal = 'Unknown command: ' line
	output = '>>>' line
	:(loop)

notcmd	output = prefix line
	:(loop)

****
twofontcmd
	rest = rest ' '
	oline = prefix

f1	rest tokpat =					:f(efont)
	ident(text)					:s(efont)
	oline = oline (ident(f1,'R') text, f1 '<' text '>')

f2	rest tokpat =					:f(efont)
	ident(text)					:s(efont)
	oline = oline (ident(f2,'R') text, f2 '<' text '>') :(f1)

efont	output = oline					:(loop)
****
onefontcmd
	rest = rest ' '
	rest tokpat =					:f(efont)
	output = prefix font '<' text '>'		:(loop)

****
shcmd	line '.SH' = '=sect'
	output = line					:(loop)
****
sscmd	line '.SS' = '=subsect'
	output = line					:(loop)
****
ppcmd	output =					:(loop)
****
lpcmd	prefix = '=item '				:(loop1)
****
bullcmd	prefix = '=bull '				:(loop1)
****
brcmd	output = '=break'				:(loop)
end
